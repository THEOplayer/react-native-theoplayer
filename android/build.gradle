buildscript {
  if (project == rootProject) {
    repositories {
      google()
      mavenCentral()
    }

    dependencies {
      classpath 'com.android.tools.build:gradle:4.2.2'
      classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.10'
    }
  }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  compileSdkVersion safeExtGet('THEOplayer_compileSdkVersion', 31)

  defaultConfig {
    minSdkVersion safeExtGet('THEOplayer_minSdkVersion', 21)
    targetSdkVersion safeExtGet('THEOplayer_targetSdkVersion', 31)
    versionCode 1
    versionName "1.0"

    // Optionally limit timeUpdate rate. Possible values:
    // - "com.theoplayer.TimeUpdateRate.UNLIMITED"
    // - "com.theoplayer.TimeUpdateRate.LIMITED_ONE_HZ"
    // - "com.theoplayer.TimeUpdateRate.LIMITED_TWO_HZ"
    // - "com.theoplayer.TimeUpdateRate.LIMITED_THREE_HZ"
    def TimeUpdateRate = "com.theoplayer.TimeUpdateRate"
    buildConfigField TimeUpdateRate, "TIMEUPDATE_RATE", safeExtGet('THEOplayer_timeUpdateRate', "${TimeUpdateRate}.UNLIMITED")

    // Optionally log events to logcat
    buildConfigField "boolean", "LOG_PLAYER_EVENTS", "${safeExtGet('THEOplayer_logPlayerEvents', 'false')}"
    buildConfigField "boolean", "LOG_VIEW_EVENTS", "${safeExtGet('THEOplayer_logViewEvents', 'false')}"

    // Extensions
    buildConfigField "boolean", "EXTENSION_GOOGLE_IMA", "${safeExtGet("THEOplayer_extensionGoogleIMA", 'false')}"
    buildConfigField "boolean", "EXTENSION_GOOGLE_DAI", "${safeExtGet("THEOplayer_extensionGoogleDAI", 'false')}"
    buildConfigField "boolean", "EXTENSION_CAST", "${safeExtGet("THEOplayer_extensionCast", 'false')}"
    buildConfigField "boolean", "EXTENSION_MEDIATAILOR", "${safeExtGet("THEOplayer_extensionMediaTailor", 'false')}"
  }

  buildTypes {
    debug {
      minifyEnabled false
    }
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    disable 'GradleCompatible'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  packagingOptions {
    exclude 'META-INF/kotlin-stdlib.kotlin_module'
    exclude 'META-INF/annotation-experimental_release.kotlin_module'
  }
}

repositories {
  mavenLocal()
  maven {
    // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
    url("$rootDir/../node_modules/react-native/android")
  }
  google()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

dependencies {
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"  // From node_modules
  implementation "com.theoplayer.theoplayer-sdk-android:unified:${safeExtGet('THEOplayer_sdk', '+')}"

  if (safeExtGet("THEOplayer_extensionGoogleIMA", false)) {
    println('ENABLE THEOplayer IMA extension.')
    implementation "com.theoplayer.theoplayer-sdk-android:unified-ads:${safeExtGet('THEOplayer_sdk', '+')}"
    implementation "com.theoplayer.theoplayer-sdk-android:unified-ads-ima:${safeExtGet('THEOplayer_sdk', '+')}"
  } else {
    println('DISABLE THEOplayer IMA extension.')
    compileOnly "com.theoplayer.theoplayer-sdk-android:unified-ads:${safeExtGet('THEOplayer_sdk', '+')}"
    compileOnly "com.theoplayer.theoplayer-sdk-android:unified-ads-ima:${safeExtGet('THEOplayer_sdk', '+')}"
  }

  // NOTE: remove stub once DAI extension module is available on Maven (v4.2)
  if (safeExtGet("THEOplayer_extensionGoogleDAI", false)) {
    println('ENABLE THEOplayer DAI extension.')
    implementation files('libs/theoplayer-android-daistub.aar')
//    implementation "com.theoplayer.theoplayer-sdk-android:unified-ads:${safeExtGet('THEOplayer_sdk', '+')}"
//    implementation "com.theoplayer.theoplayer-sdk-android:unified-ads-dai:${safeExtGet('THEOplayer_sdk', '+')}"
  } else {
    println('DISABLE THEOplayer DAI extension.')
    compileOnly files('libs/theoplayer-android-daistub.aar')
//    compileOnly "com.theoplayer.theoplayer-sdk-android:unified-ads:${safeExtGet('THEOplayer_sdk', '+')}"
//    compileOnly "com.theoplayer.theoplayer-sdk-android:unified-ads-dai:${safeExtGet('THEOplayer_sdk', '+')}"
  }

  if (safeExtGet("THEOplayer_extensionCast", false)) {
    println('ENABLE THEOplayer cast extension.')
    implementation "com.theoplayer.theoplayer-sdk-android:unified-cast:${safeExtGet('THEOplayer_sdk', '+')}"
  } else {
    println('DISABLE THEOplayer cast extension.')
    compileOnly "com.theoplayer.theoplayer-sdk-android:unified-cast:${safeExtGet('THEOplayer_sdk', '+')}"
  }
}
