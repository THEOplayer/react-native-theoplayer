buildscript {
  ext.safeExtGet = { prop, fallback ->
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:7.4.2'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${safeExtGet('kotlinVersion', '1.8.21')}"
  }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

static def versionString(version) {
  return "${version == '+' ? 'latest' : version}"
}

// Extensions
def enabledGoogleIMA = safeExtGet("THEOplayer_extensionGoogleIMA", 'false').toBoolean()
def enabledGoogleDAI = safeExtGet("THEOplayer_extensionGoogleDAI", 'false').toBoolean()
def enabledAds = enabledGoogleIMA || enabledGoogleDAI
def enabledCast = safeExtGet("THEOplayer_extensionCast", 'false').toBoolean()
def enabledMediaSession = safeExtGet("THEOplayer_extensionMediaSession", 'true').toBoolean()

android {
  compileSdk safeExtGet('THEOplayer_compileSdkVersion', 34)

  defaultConfig {
    minSdkVersion safeExtGet('THEOplayer_minSdkVersion', 21)
    targetSdkVersion safeExtGet('THEOplayer_targetSdkVersion', 34)
    versionCode 1
    versionName "1.0"

    // Optionally limit timeUpdate rate. Possible values:
    // - "com.theoplayer.TimeUpdateRate.UNLIMITED"
    // - "com.theoplayer.TimeUpdateRate.LIMITED_ONE_HZ"
    // - "com.theoplayer.TimeUpdateRate.LIMITED_TWO_HZ"
    // - "com.theoplayer.TimeUpdateRate.LIMITED_THREE_HZ"
    def TimeUpdateRate = "com.theoplayer.TimeUpdateRate"
    buildConfigField TimeUpdateRate, "TIMEUPDATE_RATE", safeExtGet('THEOplayer_timeUpdateRate', "${TimeUpdateRate}.UNLIMITED")

    // Optionally log events to logcat
    buildConfigField "boolean", "LOG_PLAYER_EVENTS", "${safeExtGet('THEOplayer_logPlayerEvents', 'false')}"
    buildConfigField "boolean", "LOG_VIEW_EVENTS", "${safeExtGet('THEOplayer_logViewEvents', 'false')}"
    buildConfigField "boolean", "LOG_MEDIASESSION_EVENTS", "${safeExtGet('THEOplayer_logMediaSessionEvents', 'false')}"
    buildConfigField "boolean", "USE_PLAYBACK_SERVICE", "${safeExtGet('THEOplayer_usePlaybackService', 'true')}"

    // Extension buildConfig fields
    buildConfigField "boolean", "EXTENSION_GOOGLE_IMA", "${enabledGoogleIMA}"
    buildConfigField "boolean", "EXTENSION_GOOGLE_DAI", "${enabledGoogleDAI}"
    buildConfigField "boolean", "EXTENSION_ADS", "${enabledAds}"
    buildConfigField "boolean", "EXTENSION_CAST", "${enabledCast}"
    buildConfigField "boolean", "EXTENSION_MEDIASESSION", "${enabledMediaSession}"
  }

  buildTypes {
    debug {
      minifyEnabled false
    }
    release {
      minifyEnabled false
    }
  }
  lint {
    disable 'GradleCompatible'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  packagingOptions {
    exclude 'META-INF/kotlin-stdlib.kotlin_module'
    exclude 'META-INF/annotation-experimental_release.kotlin_module'
  }
}

rootProject.allprojects {
  repositories {
    maven { url("$rootDir/../node_modules/react-native-theoplayer/android/local") }
  }
}

repositories {
  mavenLocal()
  maven {
    // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
    url("$rootDir/../node_modules/react-native/android")
  }
  google()
  mavenCentral()
  maven { url("local") }
  maven { url 'https://jitpack.io' }
}

dependencies {
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"  // From node_modules
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"
  implementation "androidx.appcompat:appcompat:1.6.1"

  // The minimum supported THEOplayer version is 6.0.0
  def theoplayer_sdk_version = safeExtGet('THEOplayer_sdk', '[6.0.0,7.0)')

//  def theoplayer_mediasession_version = safeExtGet('THEOplayer_mediasession', theoplayer_sdk_version)
  def theoplayer_mediasession_version = "5.11.0-local"

  println("Using THEOplayer (${versionString(theoplayer_sdk_version)})")
  implementation "com.theoplayer.theoplayer-sdk-android:core:${theoplayer_sdk_version}"

  implementation "com.theoplayer.theoplayer-sdk-android:ads-wrapper:6.10.0"

  if (enabledMediaSession) {
    println("Enable THEOplayer MediaSession extension (${versionString(theoplayer_mediasession_version)})")
    implementation "com.theoplayer.android-connector:mediasession:${theoplayer_mediasession_version}"
  } else {
    println('Disable THEOplayer MediaSession extension.')
    compileOnly "com.theoplayer.android-connector:mediasession:${theoplayer_mediasession_version}"
  }

  if (enabledGoogleIMA) {
    println('Enable THEOplayer IMA extension.')
    implementation "com.theoplayer.theoplayer-sdk-android:integration-ads:${theoplayer_sdk_version}"
    implementation "com.theoplayer.theoplayer-sdk-android:integration-ads-ima:${theoplayer_sdk_version}"
  } else {
    println('Disable THEOplayer IMA extension.')
    compileOnly "com.theoplayer.theoplayer-sdk-android:integration-ads:${theoplayer_sdk_version}"
    compileOnly "com.theoplayer.theoplayer-sdk-android:integration-ads-ima:${theoplayer_sdk_version}"
  }

  if (enabledGoogleDAI) {
    println('Enable THEOplayer DAI extension.')
    implementation "com.theoplayer.theoplayer-sdk-android:integration-ads:${theoplayer_sdk_version}"
    implementation "com.theoplayer.theoplayer-sdk-android:integration-ads-dai:${theoplayer_sdk_version}"
  } else {
    println('Disable THEOplayer DAI extension.')
    compileOnly "com.theoplayer.theoplayer-sdk-android:integration-ads:${theoplayer_sdk_version}"
    compileOnly "com.theoplayer.theoplayer-sdk-android:integration-ads-dai:${theoplayer_sdk_version}"
  }

  if (enabledCast) {
    println('Enable THEOplayer cast extension.')
    implementation "com.theoplayer.theoplayer-sdk-android:integration-cast:${theoplayer_sdk_version}"
  } else {
    println('Disable THEOplayer cast extension.')
    compileOnly "com.theoplayer.theoplayer-sdk-android:integration-cast:${theoplayer_sdk_version}"
  }
}
